import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'fabric-loom'
    id 'com.github.johnrengelman.shadow'
}

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

archivesBaseName = 'NegativityFabric-1.19.2'

dependencies {
    api parent
    
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-screen-handler-api-v1:${project.fabric_version}"

    compileOnly 'io.netty:netty-all:4.1.79.Final'
}

processResources {
    def props = [
            "version": project.version,
            "fabric_version": project.fabric_version,
            "loader_version": project.loader_version,
            "minecraft_version": project.minecraft_version,
    ]
    inputs.properties props

    filesMatching("fabric.mod.json") {
        expand props
    }
}

java {
    withSourcesJar()
}

loom {
    serverOnlyMinecraftJar()
}

task remapShadowJar(type: RemapJarTask) {
    dependsOn shadowJar
    inputFile.set(shadowJar.archiveFile)
}

shadowJar {
    archiveBaseName.set('NegativityFabric')
    archiveAppendix.set('1.19.2')
    archiveClassifier.set(null)


    dependencies {
        it.include(it.project(':common'))
    }
    
    mergeServiceFiles()
    from(project(':common').tasks.shadowJar)

    exclude('dummyThing')
    exclude('mappings/*')
}

build.dependsOn remapShadowJar

parent.tasks.named('shadowJar', ShadowJar) {
    it.from(shadowJar.outputs)
    it.manifest.inheritFrom(tasks.jar.manifest)
}