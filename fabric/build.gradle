import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'com.github.johnrengelman.shadow'
}

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
}

repositories {
    maven {
    	name 'minecraft'
    	url 'https://libraries.minecraft.net/'
    }
    maven {
    	name 'fabric'
    	url 'https://maven.fabricmc.net/'
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

archivesBaseName = 'Negativity'

dependencies {
    api project(':common')
    
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

compileJava.javaCompiler.set(
    javaToolchains.compilerFor { languageVersion = JavaLanguageVersion.of(17) }
)

java {
    withSourcesJar()
}

loom {
    serverOnlyMinecraftJar()
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

task remapShadowJar(type: RemapJarTask) {
    dependsOn shadowJar
    archiveBaseName.set('NegativityFabric')
    archiveAppendix.set('common')
    inputFile.set(shadowJar.archiveFile)
    destinationDirectory.set(layout.buildDirectory.dir('libs'))
}

shadowJar {
    archiveBaseName.set('NegativityFabric')
    archiveAppendix.set('common')
    archiveClassifier.set(null)

    dependencies {
        it.include(it.project(':common'))
    }

    mergeServiceFiles()
    from(project(':common').tasks.shadowJar)
    
    exclude('dummyThing')
    exclude('mappings/*')
    
    destinationDirectory.set(layout.buildDirectory.dir('devlibs'))
}

build.dependsOn remapShadowJar

parent.tasks.named('shadowJar', ShadowJar) {
    it.from(remapShadowJar.outputs)
    it.manifest.inheritFrom(tasks.jar.manifest)
}

task prepareServer(type: Copy) {
    from remapShadowJar.outputs
    rename { 'negativity.jar' }
    def pluginDir = System.getProperty('plugins.dir')
    if (pluginDir == null) {
        enabled(false)
    } else {
        into pluginDir
    }
}
