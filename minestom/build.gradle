import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.github.johnrengelman.shadow'
}

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['resources']
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

archivesBaseName = 'Negativity'

dependencies {
    api project(':common')

    compileOnly 'com.github.Minestom:Minestom:master-SNAPSHOT'
}

compileJava.javaCompiler.set(
    javaToolchains.compilerFor { languageVersion = JavaLanguageVersion.of(17) }
)

processResources {
    inputs.property "version", project.version

    filesMatching("extension.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}

shadowJar {
    archiveBaseName.set('negativity')
    archiveAppendix.set('minestom')
    archiveClassifier.set('dev')

    dependencies {
        it.include(it.project(':common'))
    }

    mergeServiceFiles()
    from(project(':common').tasks.shadowJar)
}

task prepareServer(type: Copy) {
    from shadowJar
    rename { 'negativity.jar' }
    def pluginDir = System.getProperty('plugins.dir')
    if (pluginDir == null) {
        enabled(false)
    } else {
        into pluginDir
    }
}
